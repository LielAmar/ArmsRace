import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

// Add shadow to collapsed compile
group 'com.lielamar.armsrace'
version '2.1'

allprojects {
    apply plugin: 'java'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://repo.codemc.io/repository/maven-public/' }
        maven { url = 'https://jitpack.io' }
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url = 'https://repo.dmulloy2.net/nexus/repository/public/' }
        maven {
            url = "http://dja.kr:55201/spigot"
            allowInsecureProtocol(true)
        }
    }

    dependencies {
        if (name.startsWith("api_")) {
            compileOnly(project(":core"))
        }
        compileOnly 'org.projectlombok:lombok:1.18.22'
        compileOnly 'me.clip:placeholderapi:2.10.10'
        compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
        testCompileOnly 'org.projectlombok:lombok:1.18.22'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
    }
}

dependencies {
    implementation project(':core')
    implementation project(':api_v1_8_R3')
    implementation project(':api_v1_9_R2')
    implementation project(':api_v1_10_R1')
    implementation project(':api_v1_11_R1')
    implementation project(':api_v1_12_R1')
    implementation project(':api_v1_13_R2')
    implementation project(':api_v1_14_R1')
    implementation project(':api_v1_15_R1')
    implementation project(':api_v1_16_R3')
    implementation project(':api_v1_17_R1')
    implementation project(':api_v1_18_R1')
    shadow project(':core')
    shadow project(':api_v1_8_R3')
    shadow project(':api_v1_9_R2')
    shadow project(':api_v1_10_R1')
    shadow project(':api_v1_11_R1')
    shadow project(':api_v1_12_R1')
    shadow project(':api_v1_13_R2')
    shadow project(':api_v1_14_R1')
    shadow project(':api_v1_15_R1')
    shadow project(':api_v1_16_R3')
    shadow project(':api_v1_17_R1')
    shadow project(':api_v1_18_R1')
}

shadowJar {
    destinationDirectory = file("allFiles")
    relocate 'com.cryptomorin.xseries', relocated('xseries')
}

private static String relocated(String pattern) {
    return "armsrace.libs.$pattern"
}

jar {
    finalizedBy(shadowJar)
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [version: project.version])
    }
}